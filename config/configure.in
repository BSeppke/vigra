AC_INIT(include/vigra/config.hxx)

AC_PREFIX_DEFAULT([`pwd`])
vigra_srcdir=`pwd`
vigra_builddir=`pwd`

# expand prefix and exec_prefix
if test "x$prefix" = "xNONE" ; then
    prefix=$ac_default_prefix
elif test "x$prefix" = "x." ; then
    prefix=`pwd`
fi

AC_CONFIG_AUX_DIR(config)

# Set version
MAJOR_VERSION=1
MINOR_VERSION=1
MICRO_VERSION=5

echo "configuring VIGRA $MAJOR_VERSION.$MINOR_VERSION.$MICRO_VERSION"

AC_SUBST(MAJOR_VERSION)dnl
AC_SUBST(MINOR_VERSION)dnl
AC_SUBST(MICRO_VERSION)dnl

AC_ARG_WITH( cc, AC_HELP_STRING([--with-cc], [your C compiler (default=gcc)]), CC=$with_cc, [if test "x$CC" = "x" ; then CC=gcc ; fi])
AC_ARG_WITH( cxx, AC_HELP_STRING([--with-cxx], [your C++ compiler (default=g++)]), CXX=$with_cxx, [if test "x$CXX" = "x" ; then CXX=g++ ; fi])

AC_CANONICAL_SYSTEM

AC_CYGWIN
AC_MINGW32

CFLAGS=-O
CXXFLAGS=-O
LIBCPPFLAGS=
VIGRA_INCLUDES='-I$(prefix)/include'
CPPFLAGS="$VIGRA_INCLUDES $CPPFLAGS"

AC_PROG_CC
AC_PROG_CXX
AM_PROG_LD
AC_SUBST(LD)
VIGRA_PROG_INSTALL

dnl-------------------------------------------------------------------
dnl                     libtool configuration
dnl-------------------------------------------------------------------
AC_ENABLE_SHARED(yes)
AC_ENABLE_STATIC(no)
AC_LIBTOOL_SETUP dnl tell autoconf about libtools prerequisites
AC_PROG_LIBTOOL
AC_LIBTOOL_CXX
LIBTOOL=`echo $LIBTOOL | sed 's,$(top_builddir),'$vigra_interactive_builddir,`

#########################################################

VIGRA_FIND_PACKAGE(tiff, tiff, tiff.h, [support import/export of tiff images [default=no]])
if test $with_tiff = "yes";
then
    TIFF_LIB="-L$with_tifflib -ltiff"
    IMPEX_DEPLIBS="$IMPEX_DEPLIBS $TIFF_LIB"
    LIBCPPFLAGS="$LIBCXXFLAGS -DHasTIFF -I$with_tiffinc"
fi

VIGRA_FIND_PACKAGE(jpeg, jpeg, jpeglib.h, [support import/export of jpeg images [default=no]])
if test $with_jpeg = "yes";
then
    JPEG_LIB="-L$with_jpeglib -ljpeg"
    IMPEX_DEPLIBS="$IMPEX_DEPLIBS $JPEG_LIB"
    LIBCPPFLAGS="$LIBCXXFLAGS -DHasJPEG -I$with_jpeginc"
fi

VIGRA_FIND_PACKAGE(fftw, fftw, fftw.h, [support fast Fourier transforms [default=no]])
if test $with_fftw = "yes";
then
    FFTW_LIB="-L$with_fftwlib -lfftw"
    FFTW_INCLUDES="-I$with_fftwinc"
    CPPFLAGS="$FFTW_INCLUDES $CPPFLAGS"
fi

#########################################################

IMPEX_LIB="-L$libdir -lvigraimpex -lm $IMPEX_DEPLIBS"
LIBS="$LIBS $IMPEX_LIB"

AC_SUBST(vigra_srcdir)
AC_SUBST(vigra_builddir)

AC_SUBST(TIFF_LIB)
AC_SUBST(JPEG_LIB)
AC_SUBST(FFTW_LIB)
AC_SUBST(FFTW_INCLUDES)
AC_SUBST(IMPEX_LIB)
AC_SUBST(IMPEX_DEPLIBS)
AC_SUBST(VIGRA_INCLUDES)

AC_SUBST(LIBCPPFLAGS)

AC_OUTPUT(config/Makefile.include config/vigra-config)
chmod +x config/vigra-config
