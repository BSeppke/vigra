AC_INIT(vigra, 1.3.1)

dnl-------------------------------------------------------------------
dnl                   vigra/interactive version
dnl-------------------------------------------------------------------

MAJOR_VERSION=1
MINOR_VERSION=3
MICRO_VERSION=1

echo "configuring VIGRA $MAJOR_VERSION.$MINOR_VERSION.$MICRO_VERSION"

AC_SUBST(MAJOR_VERSION)dnl
AC_SUBST(MINOR_VERSION)dnl
AC_SUBST(MICRO_VERSION)dnl

dnl-------------------------------------------------------------------
dnl                   determine directories
dnl-------------------------------------------------------------------

AC_PREFIX_DEFAULT([`pwd`])

vigra_builddir=`pwd`
vigra_srcdir=`expr $0 : "\(.*\)/"`

if test "x$vigra_srcdir" = "x" ; then 
    vigra_srcdir=`pwd` ; 
else
    cd $vigra_srcdir
    vigra_srcdir=`pwd`
    cd $vigra_builddir ;
fi

# expand prefix and exec_prefix
if test "x$prefix" = "xNONE" ; then
    prefix=$ac_default_prefix
elif test "x$prefix" = "x." ; then
    prefix=`pwd`
fi

AC_CONFIG_AUX_DIR($vigra_srcdir/config)

dnl-------------------------------------------------------------------
dnl              compiler and platform configuration
dnl-------------------------------------------------------------------
AC_CANONICAL_SYSTEM

dnl AC_CYGWIN seems to be part of AC_CANONICAL_SYSTEM
dnl AC_MINGW32 seems to be part of AC_CANONICAL_SYSTEM

CFLAGS=-O
CXXFLAGS=-O

AC_ARG_WITH( cc, AC_HELP_STRING([--with-cc], [your C compiler (default=gcc)]), CC=$with_cc, [if test "x$CC" = "x" ; then CC=gcc ; fi])
AC_ARG_WITH( cxx, AC_HELP_STRING([--with-cxx], [your C++ compiler (default=g++)]), CXX=$with_cxx, [if test "x$CXX" = "x" ; then CXX=g++ ; fi])

AC_PROG_CC
AC_PROG_CXX
AC_PROG_LD
VIGRA_PROG_INSTALL

LIBCPPFLAGS=
VIGRA_INCLUDES='-I$(prefix)/include'
VIGRA_INCPATH='$(prefix)/include'

dnl-------------------------------------------------------------------
dnl                     libtool configuration
dnl-------------------------------------------------------------------
AC_ENABLE_SHARED(yes)
AC_ENABLE_STATIC(yes)
AC_LIBTOOL_SETUP dnl tell autoconf about libtools prerequisites
AC_PROG_LIBTOOL
AC_LIBTOOL_CXX
LIBTOOL=`echo $LIBTOOL | sed 's,$(top_builddir),$(vigra_builddir),'`

dnl-------------------------------------------------------------------
dnl           tiff, jpeg and fftw package configurations
dnl-------------------------------------------------------------------
VIGRA_FIND_PACKAGE(tiff, tiff, tiff.h, [support import/export of tiff images])
if test $with_tiff = "yes";
then
    TIFF_LIB="$with_tifflib -ltiff"
    IMPEX_DEPLIBS="$IMPEX_DEPLIBS $TIFF_LIB"
    LIBCPPFLAGS="$LIBCPPFLAGS -DHasTIFF $tiff_cppflags"
fi

VIGRA_FIND_PACKAGE(jpeg, jpeg, jpeglib.h, [support import/export of jpeg images])
if test $with_jpeg = "yes";
then
    JPEG_LIB="$with_jpeglib -ljpeg"
    IMPEX_DEPLIBS="$IMPEX_DEPLIBS $JPEG_LIB"
    LIBCPPFLAGS="$LIBCPPFLAGS -DHasJPEG $jpeg_cppflags"
fi

VIGRA_FIND_PACKAGE(png, png, png.h, [support import/export of png images])

VIGRA_FIND_PACKAGE(zlib, z, , [zlib compression library (required for libpng)])

if test $with_png = "yes";
then
    if test $with_zlib != "yes";
    then
        AC_MSG_WARN([ zlib not found, configuring without support for dependent libpng])
        with_png="no"
    else
        PNG_LIB="$with_pnglib -lpng $with_zliblib -lz"
        IMPEX_DEPLIBS="$IMPEX_DEPLIBS $PNG_LIB"
        LIBCPPFLAGS="$LIBCPPFLAGS -DHasPNG $png_cppflags"
    fi
fi

VIGRA_FIND_PACKAGE(fftw, fftw3, fftw3.h, [support fast Fourier transforms])
if test $with_fftw = "yes";
then
    FFTW_LIB="$with_fftwlib -lfftw3"
    FFTW_INCLUDES="$fftw_cppflags"
    CPPFLAGS="$FFTW_INCLUDES $CPPFLAGS"
fi

dnl-------------------------------------------------------------------
dnl                        finish & output
dnl-------------------------------------------------------------------

POSTINSTALLCPPFLAGS="$VIGRA_INCLUDES ${CPPFLAGS}"
CPPFLAGS="-I$vigra_srcdir/include ${CPPFLAGS}"
LIBCPPFLAGS="$CPPFLAGS $LIBCPPFLAGS"
IMPEX_LIB="-L$libdir -lvigraimpex -lm $IMPEX_DEPLIBS"
LIBS="$LIBS $IMPEX_LIB"

AC_SUBST(vigra_srcdir)
AC_SUBST(vigra_builddir)

AC_SUBST(TIFF_LIB)
AC_SUBST(JPEG_LIB)
AC_SUBST(PNG_LIB)
AC_SUBST(FFTW_LIB)
AC_SUBST(RFFTW_LIB)
AC_SUBST(FFTW_INCLUDES)
AC_SUBST(IMPEX_LIB)
AC_SUBST(IMPEX_DEPLIBS)
AC_SUBST(VIGRA_INCLUDES)
AC_SUBST(VIGRA_INCPATH)
AC_SUBST(POSTINSTALLCPPFLAGS)

AC_SUBST(LIBCPPFLAGS)

AC_OUTPUT(
config/Makefile.include 
config/vigra-config 
doc/vigra/index.html 
src/Makefile 
src/impex/Makefile 
src/examples/Makefile 
Makefile)
chmod +x config/vigra-config # for src/examples

[echopath()
{
    echo "$1" \
    | sed "s,\$[({]exec_prefix[})],$exec_prefix," \
    | sed "s,\$[({]prefix[})],$prefix,"
}]

echo "configure finished."
echo ""
echo "TIFF support enabled (--with-tiff): $with_tiff"
echo "JPEG support enabled (--with-jpeg): $with_jpeg"
echo "PNG  support enabled (--with-png):  $with_png"
echo "FFTW support enabled (--with-fftw): $with_fftw"
echo ""
echopath "The vigra sources are taken from $vigra_srcdir."
echopath "The vigraimpex library    will be installed in $libdir."
echopath "The 'vigra-config' script will be installed in $bindir."
echopath "The include files         will be installed in $includedir."
echopath "The documentation         will be installed in $docdir."
